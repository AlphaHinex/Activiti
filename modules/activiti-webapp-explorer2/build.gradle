allprojects {
    repositories {
        maven {
            url "${nexusUrl}/repository/maven-public"
            credentials(PasswordCredentials) {
                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

apply plugin: 'java'
apply plugin: 'maven'

group = 'com.proper.enterprise.platform'
archivesBaseName = 'activiti-web-designer'
version = '5.22.0'

task pack(type: Jar) {
    metaInf {
        into 'resources'
        from 'src/main/webapp'
        include ('diagram-viewer/**', 'editor-app/**', 'modeler.html')
        exclude 'editor-app/app-cfg.js'
        exclude 'editor-app/configuration/toolbar-custom-actions.js'
        exclude 'editor-app/configuration/properties-custom-controllers.js'
    }
    manifest {
        attributes('Created-By': 'Gradle',
                   'Specification-Title': 'Activiti Web Designer',
                   'Specification-Version': version,
                   'Implementation-Title': 'Activiti Web Designer from Explorer',
                   'Implementation-Version': version,
                   'Implementation-URL': 'https://github.com/AlphaHinex/Activiti/tree/web-designer')
    }
}

artifacts {
    archives pack
}
compileJava.deleteAllActions()
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "${nexusUrl}/repository/maven-releases") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
            snapshotRepository(url: "${nexusUrl}/repository/maven-snapshots") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
        }
    }
}